;; author: « RickBarretto
;; year: 2024
;; licence: « MIT
;; version: 0.1.1
;; repository: « https://github.com/RickBarretto/unitt
;;
;; export: ['runTests 'test]

runTests: $[testPath :string][
    ;; description: « executes test-files inside the `testPath`
    ;;
    ;; arguments: [
    ;;      testPath: « the relative folder that contains the tests
    ;; ]
    ;;
    ;; example: {
    ;;      ; tester.art
    ;;      import {unitt}
    ;;      
    ;;      do ::
    ;;          runTests {tests}   
    ;; }

    ; Important to final statistics
    passed: 0
    failed: 0
    skipped: 0

    ; Important to return error code.
    ; If some test throw an error and don't explicitly catches it,
    ; this should not return 0 as error code, but 1, instead.
    fatalError: false

    isTestFile?: $[file :string][
        ;; description: « Checks if `file` starts with 'test' and is an .art file
        filename: last split.path file

        return and? 
            suffix? filename ".art" 
            prefix? filename "test"
    ]

    testfiles: list.recursive testPath | select => isTestFile?

    ; The tests execution
    loop testfiles 'file [ 
        print ~"\n===== |file| =====\n"

        result: execute.code ~"|sys\binary| |file|"
        output: split.lines result\output

        loop output 'line [
            print line
            if in? '✅' line -> inc 'passed
            if in? '❌' line -> inc 'failed
            if in? '⏩' line -> inc 'skipped
        ]

        ; if some test throws an error
        if result\code > 0 -> 
            fatalError: true

    ]

    print ~"\n===== Statistics =====\n"
    print ~"⏏️    TOTAL: |passed + failed| assertions"
    print ~"✅  PASSED: |passed| assertions"
    print ~"⏩ SKIPPED: |skipped| assertions"
    print ~"❌  FAILED: |failed| assertions"
    print ~"\n===== ========== =====\n"

    if or? (fatalError) (failed > 0) 
        -> exit.with: 1

]

test: $[description :string testCase :block][
    ;; description: {
    ;;    Run an unit-test, printing it's result.
    ;;    Assertions should be done with the internal function `assert :block`.
    ;; }
    ;; arguments: [
    ;;      description: « the test description
    ;;      testCase: {
    ;;          the block containing the test's logic and assertion
    ;;      }
    ;; ]
    ;; options: [
    ;;      prop: :logical « defines if a test is property-based
    ;;      skip: :logical « skip test if true
    ;; ]
    ;;
    ;; note: « `assert` is injected, and only available inside the `test`'s block.
    ;;
    ;; example: {
    ;;      ; testAppend.art
    ;;      import {unitt}
    ;;      
    ;;      do ::
    ;;          test.prop "appending with keyword or operator has the same behavior" [
    ;;              a: [a b c d e f g]
    ;;              b: [h i j k l m n]
    ;;              assert -> (append a b) = (a ++ b)
    ;;              assert -> (append b a) = (b ++ a)
    ;;          ]
    ;;          ; ✅ ~ assert that appending with keyword or operator has the same behavior 
    ;;          ; assertion : [[append a b] = [a ++ b]]
    ;;          ;
    ;;          ; ✅ ~ assert that appending with keyword or operator has the same behavior 
    ;;          ; assertion : [[append b a] = [b ++ a]]
    ;;          
    ;;          test.skip: unix? "split is working for windows's paths" [
    ;;              assert -> ["." "splited" "path"] = split.path ".\\splited\\path"
    ;;          ]
    ;;          ; # running on Windows:
    ;;          ; ✅ - assert that split is working for windows's paths 
    ;;          ; assertion : [[. splited path] = split path .\splited\path]
    ;;          ; 
    ;;          ; # running on Unix:
    ;;          ; ⏩ - assert that split is working for windows's paths 
    ;;          ; 
    ;; }

    sep: (attr 'prop)? -> "~" -> "-"
    template: ~"|sep| assert that |description|"

    skip?: attr 'skip

    assert: $[condition :block][

        passBlock: -> print [color #yellow ~"✅ |template|"]
        failBlock: -> print [color #yellow ~"❌ |template|"]
        skipBlock: [ 
            print [color #yellow ~"⏩ |template|"]
            print "     skipped! \n"
        ]

        (skip?)? -> do skipBlock [
            (do condition)? 
                passBlock
                failBlock

            print ~"     assertion : |condition|\n"
        ]
    ]

    do testCase

]

